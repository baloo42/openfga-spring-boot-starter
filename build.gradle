plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'jacoco'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'jacoco'

    group = "dev.openfga"
    version = "0.0.1-SNAPSHOT"

    java {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    repositories {
        mavenCentral()
    }

    spotless {
        format 'misc', {
            // define the files (e.g. '*.gradle', '*.md') to apply `misc` to
            target '.gitignore', '*.gradle'
            // define the steps to apply to those files
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        java {
            palantirJavaFormat()
            removeUnusedImports()
            importOrder()
        }
    }

    test {
        finalizedBy jacocoTestReport // report is always generated after tests run
    }
    
    jacocoTestReport {
        // tests are required to run before generating a JaCoCo coverage report.
        dependsOn test
    }

    tasks.register('fmt') {
        dependsOn 'spotlessApply'
    }

}
